     A* This file is part of cisco-erfassung, an application conglomerate for
     A*  management of Cisco devices on AS/400, i5/OS and IBM i.
     A*
     A* This is free software; you can redistribute it and/or modify it
     A*  under the terms of the GNU General Public License as published by the
     A*  Free Software Foundation; either version 2 of the License, or (at your
     A*  option) any later version.
     A*
     A* It is distributed in the hope that it will be useful, but WITHOUT
     A*  ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
     A*  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License
     A*  for more details.
     A*
     A* You should have received a copy of the GNU General Public License along
     A*  with it; if not, write to the Free Software Foundation, Inc., 59
     A*  Temple Place, Suite 330, Boston, MA 02111-1307 USA or get it at
     A*  http://www.gnu.org/licenses/gpl.html
     A*
     A* Globals ----------------------------------------------------------------
     A                                      REF(INVPF)
     A                                      ERRSFL
     A                                      HELP
     A                                      ALTHELP(CA01)
     A                                      HLPPNLGRP(GLOBAL SERNOHP)
     A* Subfile Screen ---------------------------------------------------------
     A          R MAINSFL                   SFL
     A  99                                  SFLNXTCHG
     A*         ##What may we do with the selected record?
     A            OPT            1A  B  9  3VALUES(' ' '5' '7' '8' '9')
     A  99                                  DSPATR(RI)
     A*         ##Pull values from the table definition itself...
     A            INV$SERNO$    16A  O  9  7
     A            HOSTNAME$     22A  O  9 25
     A            INV$NAME  R        O  9 49
     A            INV$SERNO R        H
     A            HOSTNAME  R        H
     A* Subfile Control Screen -------------------------------------------------
     A          R MAINCTL                   SFLCTL(MAINSFL)
     A                                      CHGINPDFT(LC UL)
     A*                                   ##Command Key definitions.
     A                                      CA03(03 'Verlassen')
     A                                      CA05(05 'Aktualisieren')
     A                                      CA12(12 'Abbrechen')
     A N34                                  PAGEDOWN(25)
     A N35                                  PAGEUP(26)
     A                                      HOME(27 'Suchfeld')
     A                                      VLDCMDKEY(29 'Any valid key')
     A*                                   ##Basic Settings for subfile ctrl.
     A                                      OVERLAY
     A  91                                  SFLMSGID(ERR0012 GENERICMSG 91)
     A  92                                  SFLMSGID(ERR1021 GENERICMSG 92)
     A  93                                  SFLMSGID(ERR1218 GENERICMSG 93)
     A  95                                  SFLMSGID(INF0001 GENERICMSG 95)
     A                                      SFLSIZ(&SFLSIZ)
     A                                      SFLPAG(13)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('Seriennummernsuche --
     A                                      Listendarstellung')
     A          H                           HLPARA(3 1 3 80)
     A                                      HLPPNLGRP('MAIN/POSTO' SERNOHP)
     A          H                           HLPARA(5 1 7 80)
     A                                      HLPPNLGRP('MAIN/SFLOPTS' SERNOHP)
     A          H                           HLPARA(*CNST 1)
     A                                      HLPPNLGRP('MAIN/START' SERNOHP)
     A          H                           HLPARA(*CNST 2)
     A                                      HLPPNLGRP('SFLHDR/OPT' SERNOHP)
     A          H                           HLPARA(*CNST 3)
     A                                      HLPPNLGRP('SFLHDR/SERNO' SERNOHP)
     A          H                           HLPARA(*CNST 4)
     A                                      HLPPNLGRP('SFLHDR/HOSTNAME' SERNOHP)
     A          H                           HLPARA(*CNST 5)
     A                                      HLPPNLGRP('SFLHDR/INVNAME' SERNOHP)
     A          H                           HLPARA(9 3 21 80)
     A                                      HLPPNLGRP('MAIN/SFLDTA' SERNOHP)
     A            SFLRCDNBR      3S 0H      SFLRCDNBR(CURSOR)
     A            SFLSIZ         5S 0P
     A*         ##End of definitions. Here be real screen elements.
     A                                  1 31'Seriennummernsuche'
     A                                      DSPATR(HI)
     A                                  3  7'Listenanfang bei . . .'
     A            POSTO         16A  I  3 31
     A  41                                  DSPATR(PC)
     A                                  3 50'(Seriennummer)'
     A                                  5  2'Auswahl eingeben und -
     A                                      Eingabetaste drücken.'
     A                                      COLOR(BLU)
     A                                  6  3'5=Details  7=Show running--
     A                                      configuration  8=Show version  -
     A                                      9=Show inventory'
     A                                      COLOR(BLU)
     A  35                              7 70'Beginn ...' DSPATR(HI) HLPID(1)
     A                                  8  2'Opt' DSPATR(HI) HLPID(2)
     A                                  8  7'Seriennummer' DSPATR(HI) HLPID(3)
     A                                  8 25'Hostname' DSPATR(HI) HLPID(4)
     A                                  8 49'Komponente' DSPATR(HI) HLPID(5)
     A* Footer of the form -----------------------------------------------------
     A* A subfile control record may contain elements above or below
     A* the actual subfile, but not both.
     A          R MAINBTM
     A                                      HLPTITLE('Geräteseriennummern --
     A                                      Listendarstellung')
     A          H                           HLPARA(22 69 22 80)
     A                                      HLPPNLGRP('MAIN/END' SERNOHP)
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/SFLBTM' SERNOHP)
     A                                 23  2'F1=Hilfe   -
     A                                      F3=Verlassen   -
     A                                      F5=Aktualisieren   -
     A                                      F12=Abbrechen'
     A                                      COLOR(BLU)
     A* No data instead of SFL -------------------------------------------------
     A          R MAINND                    OVERLAY
     A                                      HLPTITLE('Geräteseriennummern --
     A                                      Listendarstellung')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/SFLND' SERNOHP)
     A                                 10  7'(Keine Datensätze zum Anzeigen.)'
     A* For vim ----------------------------------------------------------------
     A* vim: syntax=dds colorcolumn=81 autoindent noignorecase
