     A* This file is part of cisco-erfassung, an application conglomerate for
     A*  management of Cisco devices on AS/400, i5/OS and IBM i.
     A*
     A* This is free software; you can redistribute it and/or modify it
     A*  under the terms of the GNU General Public License as published by the
     A*  Free Software Foundation; either version 2 of the License, or (at your
     A*  option) any later version.
     A*
     A* It is distributed in the hope that it will be useful, but WITHOUT
     A*  ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
     A*  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License
     A*  for more details.
     A*
     A* You should have received a copy of the GNU General Public License along
     A*  with it; if not, write to the Free Software Foundation, Inc., 59
     A*  Temple Place, Suite 330, Boston, MA 02111-1307 USA or get it at
     A*  http://www.gnu.org/licenses/gpl.html
     A*
     A* Globals ----------------------------------------------------------------
     A                                      REF(INVPF)
     A                                      ERRSFL
     A                                      HELP
     A                                      ALTHELP(CA01)
     A                                      DSPSIZ(*DS4)
     A                                      HLPPNLGRP(GLOBAL INVHP)
     A* Subfile Screen ---------------------------------------------------------
     A          R INVSFL                    SFL
     A*         ##Pull values from the table definition itself...
     A                                  5  3'Name  . . . .'
     A            INV$NAME  R        O  5 18
     A                                  6  3'Beschr. . . .'
     A            INV$DESCR R        O  6 18
     A                                  7  3'PID . . . . .'
     A            INV$PID   R        O  7 18
     A                                  7 40'VID . . .'
     A            INV$VID   R        O  7 51
     A                                  7 69'Serial . . .'
     A            INV$SERNO R        O  7 83
     A                                  8  3' '
     A* Subfile Control Screen -------------------------------------------------
     A          R INVCTL                    SFLCTL(INVSFL)
     A*                                   ##Command Key definitions.
     A                                      CA03(03 'Verlassen')
     A                                      CA05(05 'Aktualisieren')
     A                                      CA12(12 'Abbrechen')
     A*                                   ##Basic Settings for subfile ctrl.
     A                                      OVERLAY
     A                                      SFLSIZ(10)
     A                                      SFLPAG(5)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('Geräte-Inventar --
     A                                      Listenansicht')
     A          H                           HLPARA(3 1 3 55)
     A                                      HLPPNLGRP('MAIN/HOSTNAME' INVHP)
     A          H                           HLPARA(5 1 5 41)
     A                                      HLPPNLGRP('SFL/NAME' INVHP)
     A          H                           HLPARA(6 1 6 125)
     A                                      HLPPNLGRP('SFL/DESCR' INVHP)
     A          H                           HLPARA(7 1 7 35)
     A                                      HLPPNLGRP('SFL/PID' INVHP)
     A          H                           HLPARA(7 40 7 64)
     A                                      HLPPNLGRP('SFL/VID' INVHP)
     A          H                           HLPARA(7 69 7 106)
     A                                      HLPPNLGRP('SFL/SERNO' INVHP)
     A          H                           HLPARA(9 1 25 132)
     A                                      HLPPNLGRP('MAIN/SFLDTA' INVHP)
     A            SFLRCDNBR      3S 0H      SFLRCDNBR
     A*         ##End of definitions. Here be real screen elements.
     A                                  1 58'Geräte-Inventar' DSPATR(HI)
     A                                  3  2'Hostname . . .' DSPATR(HI)
     A            HOSTNAME$ R        O  3 18COLOR(TRQ) REFFLD(HOSTNAME)
     A* Footer of the form -----------------------------------------------------
     A* A subfile control record may contain elements above or below the actual
     A* subfile, but not both. So here is a simple screen overlay to add below
     A* the subfile display.
     A          R INVBTM
     A                                      HLPTITLE('Geräte-Inventar --
     A                                      Listenansicht')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/SFLBTM' INVHP)
     A                                 26  2'F3=Verlassen   -
     A                                      F5=Aktualisieren   -
     A                                      F12=Abbrechen'
     A                                      COLOR(BLU)
     A* No data instead of SFL -------------------------------------------------
     A          R INVND                     OVERLAY
     A                                      HLPTITLE('Geräte-Inventar --
     A                                      Listenansicht')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/SFLND' INVHP)
     A                                  6  2'Keine Datensätze für gewünschten -
     A                                      Hostnamen gefunden.'
     A* For vim ----------------------------------------------------------------
     A* vim: syntax=dds colorcolumn=81 autoindent noignorecase
