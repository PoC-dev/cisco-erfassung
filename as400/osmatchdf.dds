     A* This file is part of cisco-erfassung, an application conglomerate for
     A*  management of Cisco devices on AS/400, i5/OS and IBM i.
     A*
     A* This is free software; you can redistribute it and/or modify it
     A*  under the terms of the GNU General Public License as published by the
     A*  Free Software Foundation; either version 2 of the License, or (at your
     A*  option) any later version.
     A*
     A* It is distributed in the hope that it will be useful, but WITHOUT
     A*  ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
     A*  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License
     A*  for more details.
     A*
     A* You should have received a copy of the GNU General Public License along
     A*  with it; if not, write to the Free Software Foundation, Inc., 59
     A*  Temple Place, Suite 330, Boston, MA 02111-1307 USA or get it at
     A*  http://www.gnu.org/licenses/gpl.html
     A*
     A* Globals ----------------------------------------------------------------
     A                                      REF(OSMATCHPF)
     A                                      ERRSFL
     A                                      HELP
     A                                      ALTHELP(CA01)
     A                                      HLPPNLGRP(GLOBAL OSMATCHHP)
     A* Subfile Screen ---------------------------------------------------------
     A          R MAINSFL                   SFL
     A  99                                  SFLNXTCHG
     A*         ##What may we do with the selected record?
     A            OPT            1A  B  7  3VALUES(' ' '2' '3' '4' '5' '6')
     A  99                                  DSPATR(RI)
     A*         ##Pull values from the table definition itself...
     A            VERSION$      19A  O  7  7
     A            MODEL     R        O  7 28
     A            RAM       R     Y  O  7 50EDTCDE(1)
     A                                  7 56'MB'
     A            FLASH     R     Y  O  7 60EDTCDE(1)
     A                                  7 67'MB'
     A            EOS       R     L  O  7 71DATFMT(*ISO)
     A  36                                  DSPATR(ND)
     A  37                                  COLOR(RED)
     A            ID        R        H
     A* Subfile Control Screen -------------------------------------------------
     A          R MAINCTL                   SFLCTL(MAINSFL)
     A*                                   ##Command Key definitions.
     A                                      CA03(03 'Verlassen')
     A                                      CA05(05 'Aktualisieren')
     A                                      CA06(06 'Neu')
     A                                      CA11(11 'Sortierung')
     A                                      CA12(12 'Abbrechen')
     A                                      VLDCMDKEY(29 'Any valid key')
     A*                                   ##Basic Settings for subfile ctrl.
     A                                      OVERLAY
     A  91                                  SFLMSGID(ERR0012 GENERICMSG 91)
     A  92                                  SFLMSGID(ERR1021 GENERICMSG 92)
     A  93                                  SFLMSGID(ERR1218 GENERICMSG 93)
     A  95                                  SFLMSGID(INF0001 GENERICMSG 95)
     A  96                                  SFLMSGID(INF0999 GENERICMSG 96)
     A                                      SFLSIZ(30)
     A                                      SFLPAG(15)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&SFLCSRRRN)
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('OS-Geräte-Zuordnungen --
     A                                      Listendarstellung')
     A          H                           HLPARA(3 1 4 80)
     A                                      HLPPNLGRP('MAIN/SFLOPTS' OSMATCHHP)
     A          H                           HLPARA(*CNST 1)
     A                                      HLPPNLGRP('SFLHDR/OPT' OSMATCHHP)
     A          H                           HLPARA(*CNST 2)
     A                                      HLPPNLGRP('SFLHDR/VERSION'
     A                                      OSMATCHHP)
     A          H                           HLPARA(*CNST 3)
     A                                      HLPPNLGRP('SFLHDR/MODEL' OSMATCHHP)
     A          H                           HLPARA(*CNST 4)
     A                                      HLPPNLGRP('SFLHDR/RAM' OSMATCHHP)
     A          H                           HLPARA(*CNST 5)
     A                                      HLPPNLGRP('SFLHDR/FLASH' OSMATCHHP)
     A          H                           HLPARA(*CNST 6)
     A                                      HLPPNLGRP('SFLHDR/EOS' OSMATCHHP)
     A          H                           HLPARA(7 1 21 80)
     A                                      HLPPNLGRP('MAIN/SFLDTA' OSMATCHHP)
     A            SFLRCDNBR      3S 0H      SFLRCDNBR(CURSOR)
     A            SFLCSRRRN      5S 0H
     A*         ##End of definitions. Here be real screen elements.
     A                                  1 29'OS-Geräte-Zuordnungen'
     A                                      DSPATR(HI)
     A                                  3  2'Auswahl eingeben und -
     A                                      Eingabetaste drücken.'
     A                                      COLOR(BLU)
     A                                  4  3'2=Ändern  3=Duplizieren  -
     A                                      4=Löschen  5=Anzeigen  6=Version -
     A                                      ersetzen' COLOR(BLU)
     A                                  6  2'Opt' DSPATR(HI) HLPID(1)
     A                                  6  7'Version' DSPATR(HI) HLPID(2)
     A  50                                  DSPATR(UL)
     A                                  6 28'Modell' DSPATR(HI) HLPID(3)
     A  51                                  DSPATR(UL)
     A                                  6 52'RAM' DSPATR(HI) HLPID(4)
     A  52                                  DSPATR(UL)
     A                                  6 61'Flash' DSPATR(HI) HLPID(5)
     A  53                                  DSPATR(UL)
     A                                  6 71'EOS' DSPATR(HI) HLPID(6)
     A  54                                  DSPATR(UL)
     A* Footer of the form -----------------------------------------------------
     A* A subfile control record may contain elements above or below the actual
     A* subfile, but not both.
     A          R MAINBTM
     A                                      HLPTITLE('OS-Geräte-Zuordnungen --
     A                                      Listendarstellung')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/SFLBTM' OSMATCHHP)
     A                                 23  2'F1=Hilfe  -
     A                                      F3=Verlassen  -
     A                                      F5=Aktualisieren  -
     A                                      F6=Hinzufügen  -
     A                                      F11=Sort.   -
     A                                      F12=Abbr.'
     A                                      COLOR(BLU)
     A* No data instead of SFL -------------------------------------------------
     A          R MAINND                    OVERLAY
     A                                      HLPTITLE('OS-Geräte-Zuordnungen --
     A                                      Listendarstellung')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/SFLND' OSMATCHHP)
     A                                  8  7'(Keine Datensätze zum Anzeigen.)'
     A* ------------------------------------------------------------------------
     A* Subfile Screen: Delete confirmation ------------------------------------
     A          R DLTSFL                    SFL
     A                                      SFLNXTCHG
     A            DOPT           1A  B  7  3VALUES(' ' '4')
     A  99                                  DSPATR(RI)
     A*         ##Pull values from the table definition itself...
     A            VERSION$  R        O  7  7REFFLD(MAINSFL/VERSION$ *SRC)
     A            MODEL     R        O  7 28
     A            RAM       R     Y  O  7 50EDTCDE(1)
     A                                  7 56'MB'
     A            FLASH     R     Y  O  7 60EDTCDE(1)
     A                                  7 67'MB'
     A            EOS       R     L  O  7 71DATFMT(*ISO)
     A  36                                  DSPATR(ND)
     A  37                                  COLOR(RED)
     A            ID        R        H
     A* Subfile Control Screen -------------------------------------------------
     A          R DLTCTL                    SFLCTL(DLTSFL)
     A*                                   ##Command Key definitions.
     A                                      CA12(12 'Abbrechen')
     A                                      VLDCMDKEY(29 'Any valid key')
     A*                                   ##Basic Settings for subfile ctrl.
     A                                      OVERLAY
     A  91                                  SFLMSGID(ERR0012 GENERICMSG 91)
     A  93                                  SFLMSGID(ERR1218 GENERICMSG 93)
     A  96                                  SFLMSGID(INF0999 GENERICMSG 96)
     A                                      SFLSIZ(30)
     A                                      SFLPAG(15)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('OS-Geräte-Zuordnungen --
     A                                      Löschabfrage')
     A          H                           HLPARA(3 1 4 80)
     A                                      HLPPNLGRP('DELETE/SFLOPTS' SFLDLTHP)
     A          H                           HLPARA(*CNST 1)
     A                                      HLPPNLGRP('DELETE/OPT' SFLDLTHP)
     A          H                           HLPARA(6 7 6 80)
     A                                      HLPPNLGRP('DELETE/FIELDS' SFLDLTHP)
     A          H                           HLPARA(7 1 21 80)
     A                                      HLPPNLGRP('DELETE/SFLDTA' SFLDLTHP)
     A            SFLDLTNBR      3S 0H      SFLRCDNBR(CURSOR)
     A*         ##End of definitions. Here be real screen elements.
     A                                  1 29'Löschen von Einträgen'
     A                                      DSPATR(HI)
     A                                  3  2'Auswahl ggfs. anpassen und -
     A                                      Eingabetaste drücken.'
     A                                      COLOR(BLU)
     A                                  4  3'4=Löschen  Leer=Nicht löschen'
     A                                      COLOR(BLU)
     A                                  6  2'Opt' DSPATR(HI) HLPID(1)
     A                                  6  7'Version' DSPATR(HI)
     A                                  6 28'Modell' DSPATR(HI)
     A                                  6 52'RAM' DSPATR(HI)
     A                                  6 61'Flash' DSPATR(HI)
     A                                  6 71'EOS' DSPATR(HI)
     A* Footer of the form -----------------------------------------------------
     A* A subfile control record may contain elements above or below the actual
     A* subfile, but not both.
     A          R DLTBTM
     A                                      HLPTITLE('OS-Geräte-Zuordnungen --
     A                                      Löschabfrage')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('DELETE/SFLBTM' SFLDLTHP)
     A                                 23  2'F1=Hilfe   F12=Abbrechen'
     A                                      COLOR(BLU)
     A* ------------------------------------------------------------------------
     A* ------------------------------------------------------------------------
     A* One form for displaying further details, or editing existing, or adding
     A* new records. We can switch this form to "looks like output only fields"
     A* with IN44.
     A          R DETAILFRM
     A                                      CHGINPDFT(LC UL)
     A                                      CHANGE(28 'Content has changed')
     A                                      CA03(03 'Verlassen')
     A                                      CA12(12 'Abbrechen')
     A                                      RTNCSRLOC(*RECNAME &CSREC &CSFLD)
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('OS-Geräte-Zuordnungen --
     A                                      Details')
     A          H                           HLPARA(1 1 1 80)
     A                                      HLPPNLGRP('DETAIL/MODE' OSMATCHHP)
     A          H                           HLPARA(3 1 3 80)
     A                                      HLPPNLGRP('DETAIL/OPTIONS'
     A                                      OSMATCHHP)
     A          H                           HLPARA(5 1 5 36)
     A                                      HLPPNLGRP('DFIELD/VERSION'
     A                                      OSMATCHHP)
     A          H                           HLPARA(7 1 7 36)
     A                                      HLPPNLGRP('DFIELD/MODEL' OSMATCHHP)
     A          H                           HLPARA(9 1 9 25)
     A                                      HLPPNLGRP('DFIELD/RAM' OSMATCHHP)
     A          H                           HLPARA(11 1 11 25)
     A                                      HLPPNLGRP('DFIELD/FLASH' OSMATCHHP)
     A          H                           HLPARA(13 1 13 26)
     A                                      HLPPNLGRP('DFIELD/EOS' OSMATCHHP)
     A          H                           HLPARA(15 1 15 32)
     A                                      HLPPNLGRP('DFIELD/CHANGED'
     A                                      OSMATCHHP)
     A          H                           HLPARA(23 1 23 80)
     A                                      HLPPNLGRP('DETAIL/FKEYS' OSMATCHHP)
     A*         ##End of definitions. Here be real screen elements.
     A  42N43                           1 30'Neuen Eintrag erfassen'
     A                                      DSPATR(HI)
     A N42 43                           1 32'Eintrag ändern'
     A                                      DSPATR(HI)
     A N42N43                           1 31'Eintrag anzeigen'
     A                                      DSPATR(HI)
     A  42N43                           3  2'Felder ausfüllen und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A N42 43                           3  2'Felder anpassen und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A N42N43                           3  2'Details:'
     A                                      COLOR(BLU)
     A                                  5  3'Version . . .'
     A            VERSION   R        B  5 17
     A  44                                  DSPATR(PR)
     A N44                                  DSPATR(UL)
     A N44 60                               DSPATR(PC)
     A  92                                  ERRMSGID(ERR1021 GENERICMSG 92)
     A                                  7  3'Modell  . . .'
     A            MODEL     R        B  7 17
     A  44                                  DSPATR(PR)
     A N44                                  DSPATR(UL)
     A N44 61                               DSPATR(PC)
     A                                  9  3'RAM . . . . .'
     A            RAM       R     Y  B  9 18EDTCDE(1)
     A  44                                  DSPATR(PR)
     A N44                                  DSPATR(UL)
     A N44 62                               DSPATR(PC)
     A                                  9 24'MB'
     A                                 11  3'Flash . . . .'
     A            FLASH     R     Y  B 11 17EDTCDE(1)
     A  44                                  DSPATR(PR)
     A N44                                  DSPATR(UL)
     A N44 63                               DSPATR(PC)
     A                                 11 24'MB'
     A N46                             13  3'EOS . . . . .'
     A            EOS$          10A  B 13 17
     A  44                                  DSPATR(PR)
     A N44                                  DSPATR(UL)
     A N44 64                               DSPATR(PC)
     A N44 46                               DSPATR(ND)
     A  44 47                               COLOR(RED)
     A                                 15  3'Geändert  . .'
     A            STAMP$        16A  O 15 17
     A                                 23  2'F1=Hilfe   F3=Verlassen   -
     A                                      F12=Abbrechen'
     A                                      COLOR(BLU)
     A* This is entirely to prevent DSPATR(RI) error indication with just a
     A*  warning message. Place in least used screen position.
     A            DUMMY$$        1A  O 23 80DSPATR(ND)
     A  91                                  ERRMSGID(ERR0012 GENERICMSG 91)
     A  93                                  ERRMSGID(ERR1218 GENERICMSG 93)
     A  94                                  ERRMSGID(RDO1218 GENERICMSG 94)
     A  95                                  ERRMSGID(INF0001 GENERICMSG 95)
     A            CSREC         10A  H
     A            CSFLD         10A  H
     A* Replace Window ---------------------------------------------------------
     A* One form in a window for replacing a version string over all hosts.
     A* Good for upgrades.
     A          R REPVERFRM                 WINDOW(*DFT 8 42 *NOMSGLIN)
     A                                      CHGINPDFT(UL LC)
     A                                      CHANGE(28 'Form has changed data')
     A                                      CA03(03 'Verlassen')
     A                                      CA12(12 'Abbrechen')
     A                                      HLPTITLE('OS-Geräte-Zuordnungen --
     A                                      Version Ersetzen')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/RPLVER' ACMATCHHP)
     A                                  1  1'Alle Einträge ändern (Version):'
     A                                      DSPATR(HI)
     A                                  3  1'Felder ausfüllen und -
     A                                      Eingabetaste drücken.'
     A                                      COLOR(BLU)
     A                                  5  1'Von Version'
     A            VERS#OLD  R        O  5 15REFFLD(VERSION CISCO/DCAPF)
     A                                  6  1'Nach Version'
     A            VERS#NEW  R        B  6 15REFFLD(VERSION CISCO/DCAPF)
     A                                  8  6'F1=Hilfe  F3=Verlassen  -
     A                                      F12=Abbrechen'
     A                                      COLOR(BLU)
     A* For vim ----------------------------------------------------------------
     A* vim: syntax=dds colorcolumn=81 autoindent noignorecase
