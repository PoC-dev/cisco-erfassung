     A* This file is part of cisco-erfassung, an application conglomerate for
     A*  management of Cisco devices on AS/400, i5/OS and IBM i.
     A*
     A* This is free software; you can redistribute it and/or modify it
     A*  under the terms of the GNU General Public License as published by the
     A*  Free Software Foundation; either version 2 of the License, or (at your
     A*  option) any later version.
     A*
     A* It is distributed in the hope that it will be useful, but WITHOUT
     A*  ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
     A*  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License
     A*  for more details.
     A*
     A* You should have received a copy of the GNU General Public License along
     A*  with it; if not, write to the Free Software Foundation, Inc., 59
     A*  Temple Place, Suite 330, Boston, MA 02111-1307 USA or get it at
     A*  http://www.gnu.org/licenses/gpl.html
     A*
     A* Globals ----------------------------------------------------------------
     A                                      REF(CISCO/CFGVERPF)
     A                                      ERRSFL
     A                                      HELP
     A                                      ALTHELP(CA01)
     A                                      DSPSIZ(*DS4)
     A                                      HLPPNLGRP(GLOBAL CFGVERHP)
     A* Subfile Screen ---------------------------------------------------------
     A          R CFGSFL                    SFL
     A*         ##Pull values from the table definition itself...
     A            LINE      R           6  2
     A* Subfile Control Screen -------------------------------------------------
     A          R CFGCTL                    SFLCTL(CFGSFL)
     A*                                   ##Command Key definitions.
     A                                      CA03(03 'Verlassen')
     A                                      CA05(05 'Aktualisieren')
     A                                      CA12(12 'Abbrechen')
     A*                                   ##Basic Settings for subfile ctrl.
     A                                      OVERLAY
     A  96                                  SFLMSGID(INF0999 GENERICMSG 96)
     A                                      SFLSIZ(95)
     A                                      SFLPAG(19)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('Textbetrachter')
     A          H                           HLPARA(1 1 1 132)
     A                                      HLPPNLGRP('MAIN/HEADING' CFGVERHP)
     A          H                           HLPARA(3 1 3 59)
     A                                      HLPPNLGRP('MAIN/HOSTNAME' CFGVERHP)
     A          H                           HLPARA(4 1 4 49)
     A                                      HLPPNLGRP('MAIN/CFSAVD' CFGVERHP)
     A          H                           HLPARA(6 1 24 132)
     A                                      HLPPNLGRP('MAIN/SFLDTA' CFGVERHP)
     A            SFLRCDNBR      4S 0H      SFLRCDNBR
     A*         ##End of definitions. Here be real screen elements.
     A*         ##*IN40 = Show Version, ! *IN40 = Config.
     A N40                              1 59'Konfiguration' DSPATR(HI)
     A  40                              1 60'Show Version' DSPATR(HI)
     A                                  3  2'Hostname . . . . .' DSPATR(HI)
     A            HOSTNAME$ R           3 22REFFLD(HOSTNAME CFGVERPF) COLOR(TRQ)
     A  41                              4  3'Zeitstempel . . .'
     A  41        CFSAVD$       16A     4 22
     A  42                              4 67'Gezeigte ASA-Konfiguration ist -
     A                                      die laufende, nicht die gesicherte.'
     A* Footer of the form -----------------------------------------------------
     A* A subfile control record may contain elements above or below
     A* the actual subfile, but not both. So here is a simple screen
     A* overlay to add below the subfile display.
     A          R CFGBTM
     A                                      HLPTITLE('Textbetrachter')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/SFLBTM' CFGVERHP)
     A                                 26  2'F3=Verlassen   -
     A                                      F5=Aktualisieren   -
     A                                      F12=Abbrechen'
     A                                      COLOR(BLU)
     A* No data instead of SFL -------------------------------------------------
     A          R CFGND                     OVERLAY
     A                                      HLPTITLE('Textbetrachter')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/SFLND' CFGVERHP)
     A                                  7  2'Keine Datensätze für gewünschten -
     A                                      Hostnamen und Ausgabetyp gefunden.'
     A* For vim ----------------------------------------------------------------
     A* vim: syntax=dds colorcolumn=81 autoindent noignorecase
