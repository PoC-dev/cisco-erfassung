     A* This file is part of cisco-erfassung, an application conglomerate for
     A*  management of Cisco devices on AS/400, i5/OS and IBM i.
     A*
     A* This is free software; you can redistribute it and/or modify it
     A*  under the terms of the GNU General Public License as published by the
     A*  Free Software Foundation; either version 2 of the License, or (at your
     A*  option) any later version.
     A*
     A* It is distributed in the hope that it will be useful, but WITHOUT
     A*  ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
     A*  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License
     A*  for more details.
     A*
     A* You should have received a copy of the GNU General Public License along
     A*  with it; if not, write to the Free Software Foundation, Inc., 59
     A*  Temple Place, Suite 330, Boston, MA 02111-1307 USA or get it at
     A*  http://www.gnu.org/licenses/gpl.html
     A*
     A* Globals ----------------------------------------------------------------
     A                                      REF(ACMATCHPF)
     A                                      ERRSFL
     A                                      HELP
     A                                      ALTHELP(CA01)
     A                                      HLPPNLGRP(GLOBAL ACMATCHHP)
     A* Subfile Screen ---------------------------------------------------------
     A          R MAINSFL                   SFL
     A  99                                  SFLNXTCHG
     A*         ##What may we do with the selected record?
     A            OPT            1A  B  7  3VALUES(' ' '2' '3' '4' '5' '6')
     A  99                                  DSPATR(RI)
     A*         ##Pull values from the table definition itself...
     A            AC_TYPE   R        O  7  7
     A            AC_VER    R        O  7 25
     A* Subfile Control Screen -------------------------------------------------
     A          R MAINCTL                   SFLCTL(MAINSFL)
     A*                                   ##Command Key definitions.
     A                                      CA03(03 'Verlassen')
     A                                      CA05(05 'Aktualisieren')
     A                                      CA06(06 'Neu')
     A                                      CA12(12 'Abbrechen')
     A                                      VLDCMDKEY(29 'Any valid key')
     A*                                   ##Basic Settings for subfile ctrl.
     A                                      OVERLAY
     A  91                                  SFLMSGID(ERR0012 GENERICMSG 91)
     A  92                                  SFLMSGID(ERR1021 GENERICMSG 92)
     A  93                                  SFLMSGID(ERR1218 GENERICMSG 93)
     A  95                                  SFLMSGID(INF0001 GENERICMSG 95)
     A  96                                  SFLMSGID(INF0999 GENERICMSG 96)
     A                                      SFLSIZ(30)
     A                                      SFLPAG(15)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&SFLCSRRRN)
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('Anyconnect-Zuordnungen - -
     A                                      Listendarstellung')
     A          H                           HLPARA(3 1 4 80)
     A                                      HLPPNLGRP('MAIN/SFLOPTS' ACMATCHHP)
     A          H                           HLPARA(*CNST 1)
     A                                      HLPPNLGRP('SFLHDR/OPT' ACMATCHHP)
     A          H                           HLPARA(*CNST 2)
     A                                      HLPPNLGRP('SFLHDR/AC_TYPE'
     A                                      ACMATCHHP)
     A          H                           HLPARA(*CNST 3)
     A                                      HLPPNLGRP('SFLHDR/AC_VER' ACMATCHHP)
     A          H                           HLPARA(7 1 21 80)
     A                                      HLPPNLGRP('MAIN/SFLDTA' ACMATCHHP)
     A            SFLRCDNBR      3S 0H      SFLRCDNBR(CURSOR)
     A            SFLCSRRRN      5S 0H
     A*         ##End of definitions. Here be real screen elements.
     A                                  1 29'Anyconnect-Zuordnungen'
     A                                      DSPATR(HI)
     A                                  3  2'Auswahl eingeben und -
     A                                      Eingabetaste drücken.'
     A                                      COLOR(BLU)
     A                                  4  3'2=Ändern  3=Duplizieren  -
     A                                      4=Löschen  5=Anzeigen  6=Version -
     A                                      ersetzen' COLOR(BLU)
     A                                  6  2'Opt' DSPATR(HI) HLPID(1)
     A                                  6  7'Plattform' DSPATR(HI) HLPID(2)
     A                                  6 25'Version' DSPATR(HI) HLPID(3)
     A* Footer of the form -----------------------------------------------------
     A* A subfile control record may contain elements above or below the actual
     A* subfile, but not both.
     A          R MAINBTM
     A                                      HLPTITLE('Anyconnect-Zuordnungen - -
     A                                      Listendarstellung')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/SFLBTM' ACMATCHHP)
     A                                 23  2'F1=Hilfe   -
     A                                      F3=Verlassen   -
     A                                      F5=Aktualisieren   -
     A                                      F6=Neu   -
     A                                      F12=Abbrechen'
     A                                      COLOR(BLU)
     A* No data instead of SFL -------------------------------------------------
     A          R MAINND                    OVERLAY
     A                                      HLPTITLE('Anyconnect-Zuordnungen - -
     A                                      Listendarstellung')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/SFLND' ACMATCHHP)
     A                                  8  7'(Keine Datensätze zum Anzeigen.)'
     A* ------------------------------------------------------------------------
     A* Subfile Screen: Delete confirmation ------------------------------------
     A          R DLTSFL                    SFL
     A                                      SFLNXTCHG
     A            DOPT           1A  B  7  3VALUES(' ' '4')
     A  99                                  DSPATR(RI)
     A*         ##Pull values from the table definition itself...
     A            AC_TYPE   R        O  7  7
     A            AC_VER    R        O  7 25
     A* Subfile Control Screen -------------------------------------------------
     A          R DLTCTL                    SFLCTL(DLTSFL)
     A*                                   ##Command Key definitions.
     A                                      CA12(12 'Abbrechen')
     A                                      VLDCMDKEY(29 'Any valid key')
     A*                                   ##Basic Settings for subfile ctrl.
     A                                      OVERLAY
     A  91                                  SFLMSGID(ERR0012 GENERICMSG 91)
     A  93                                  SFLMSGID(ERR1218 GENERICMSG 93)
     A  96                                  SFLMSGID(INF0999 GENERICMSG 96)
     A                                      SFLSIZ(30)
     A                                      SFLPAG(15)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('Anyconnect-Zuordnungen - -
     A                                      Löschabfrage')
     A          H                           HLPARA(3 1 4 80)
     A                                      HLPPNLGRP('DELETE/SFLOPTS' SFLDLTHP)
     A          H                           HLPARA(*CNST 1)
     A                                      HLPPNLGRP('DELETE/OPT' SFLDLTHP)
     A          H                           HLPARA(6 7 6 80)
     A                                      HLPPNLGRP('DELETE/FIELDS' SFLDLTHP)
     A          H                           HLPARA(7 1 21 80)
     A                                      HLPPNLGRP('DELETE/SFLDTA' SFLDLTHP)
     A            SFLDLTNBR      3S 0H      SFLRCDNBR(CURSOR)
     A*         ##End of definitions. Here be real screen elements.
     A                                  1 29'Löschen von Einträgen'
     A                                      DSPATR(HI)
     A                                  3  2'Auswahl ggfs. anpassen und -
     A                                      Eingabetaste drücken.'
     A                                      COLOR(BLU)
     A                                  4  3'4=Löschen  Leer=Nicht löschen'
     A                                      COLOR(BLU)
     A                                  6  2'Opt' DSPATR(HI) HLPID(1)
     A                                  6  7'Plattform' DSPATR(HI)
     A                                  6 13'Version' DSPATR(HI)
     A* Footer of the form -----------------------------------------------------
     A* A subfile control record may contain elements above or below the actual
     A* subfile, but not both.
     A          R DLTBTM
     A                                      HLPTITLE('Anyconnect-Zuordnungen - -
     A                                      Löschabfrage')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('DELETE/SFLBTM'
     A                                      SFLDLTHP)
     A                                 23  2'F1=Hilfe   F12=Abbrechen'
     A                                      COLOR(BLU)
     A* ------------------------------------------------------------------------
     A* ------------------------------------------------------------------------
     A* One form for displaying further details, or editing existing, or adding
     A* new records. We can switch this form to "looks like output only fields"
     A* with IN44.
     A          R DETAILFRM
     A                                      CHGINPDFT(LC UL)
     A                                      CHANGE(28 'Content has changed')
     A                                      CA03(03 'Verlassen')
     A                                      CA12(12 'Abbrechen')
     A                                      RTNCSRLOC(*RECNAME &CSREC &CSFLD)
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('Anyconnect-Zuordnungen - -
     A                                      Details')
     A          H                           HLPARA(1 1 1 80)
     A                                      HLPPNLGRP('DETAIL/MODE' ACMATCHHP)
     A          H                           HLPARA(3 1 3 80)
     A                                      HLPPNLGRP('DETAIL/OPTIONS'
     A                                      ACMATCHHP)
     A          H                           HLPARA(5 1 5 35)
     A                                      HLPPNLGRP('DFIELD/AC_TYPE'
     A                                      ACMATCHHP)
     A          H                           HLPARA(7 1 7 35)
     A                                      HLPPNLGRP('DFIELD/AC_VER' ACMATCHHP)
     A          H                           HLPARA(9 1 9 38)
     A                                      HLPPNLGRP('DFIELD/CHANGED'
     A                                      ACMATCHHP)
     A          H                           HLPARA(23 1 23 80)
     A                                      HLPPNLGRP('DETAIL/FKEYS' ACMATCHHP)
     A*         ##End of definitions. Here be real screen elements.
     A  42N43                           1 30'Neuen Eintrag erfassen'
     A                                      DSPATR(HI)
     A N42 43                           1 32'Eintrag ändern'
     A                                      DSPATR(HI)
     A N42N43                           1 31'Eintrag anzeigen'
     A                                      DSPATR(HI)
     A  42N43                           3  2'Felder ausfüllen und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A N42 43                           3  2'Felder anpassen und Eingabetaste -
     A                                      drücken.'
     A                                      COLOR(BLU)
     A N42N43                           3  2'Details:'
     A                                      COLOR(BLU)
     A                                  5  3'Plattform . . .'
     A            AC_TYPE   R        B  5 20
     A  44                                  DSPATR(PR)
     A N44                                  DSPATR(UL)
     A N44 60                               DSPATR(PC)
     A  92                                  ERRMSGID(ERR1021 GENERICMSG 92)
     A                                  7  3'Version . . . .'
     A            AC_VER    R        B  7 20
     A  44                                  DSPATR(PR)
     A N44                                  DSPATR(UL)
     A N44 61                               DSPATR(PC)
     A                                  9  3'Geändert  . . .'
     A            STAMP$        16A  O  9 20
     A                                 23  2'F1=Hilfe   F3=Verlassen   -
     A                                      F12=Abbrechen'
     A                                      COLOR(BLU)
     A* This is entirely to prevent DSPATR(RI) error indication with just a
     A*  warning message. Place in least used screen position.
     A            DUMMY$$        1A  O 23 80DSPATR(ND)
     A  91                                  ERRMSGID(ERR0012 GENERICMSG 91)
     A  93                                  ERRMSGID(ERR1218 GENERICMSG 93)
     A  94                                  ERRMSGID(RDO1218 GENERICMSG 94)
     A  95                                  ERRMSGID(INF0001 GENERICMSG 95)
     A            CSREC         10A  H
     A            CSFLD         10A  H
     A* Replace Window ---------------------------------------------------------
     A* One form in a window for replacing a version string over all hosts.
     A* Good for upgrades.
     A          R REPVERFRM                 WINDOW(*DFT 8 42 *NOMSGLIN)
     A                                      CHGINPDFT(UL LC)
     A                                      CHANGE(28 'Form has changed data')
     A                                      CA03(03 'Verlassen')
     A                                      CA12(12 'Abbrechen')
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('Anyconnect-Zuordnungen - -
     A                                      Version Ersetzen')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('MAIN/RPLVER' ACMATCHHP)
     A                                  1  1'Alle Einträge ändern (Version):'
     A                                      DSPATR(HI)
     A                                  3  1'Felder ausfüllen und -
     A                                      Eingabetaste drücken.'
     A                                      COLOR(BLU)
     A                                  5  1'Von Version'
     A            VERS#OLD  R        O  5 15REFFLD(AC_VER CISCO/ACMATCHPF)
     A                                  6  1'Nach Version'
     A            VERS#NEW  R        B  6 15REFFLD(AC_VER CISCO/ACMATCHPF)
     A                                  8  6'F1=Hilfe  F3=Verlassen  -
     A                                      F12=Abbrechen'
     A                                      COLOR(BLU)
     A* For vim ----------------------------------------------------------------
     A* vim: syntax=dds colorcolumn=81 autoindent noignorecase
