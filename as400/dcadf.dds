     A* This file is part of cisco-erfassung, an application conglomerate for
     A*  management of Cisco devices on AS/400, i5/OS and IBM i.
     A*
     A* This is free software; you can redistribute it and/or modify it
     A*  under the terms of the GNU General Public License as published by the
     A*  Free Software Foundation; either version 2 of the License, or (at your
     A*  option) any later version.
     A*
     A* It is distributed in the hope that it will be useful, but WITHOUT
     A*  ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
     A*  FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License
     A*  for more details.
     A*
     A* You should have received a copy of the GNU General Public License along
     A*  with it; if not, write to the Free Software Foundation, Inc., 59
     A*  Temple Place, Suite 330, Boston, MA 02111-1307 USA or get it at
     A*  http://www.gnu.org/licenses/gpl.html
     A*
     A* Globals-----------------------------------------------------------------
     A                                      REF(DCAPF)
     A                                      ERRSFL
     A                                      HELP
     A                                      ALTHELP(CA01)
     A                                      HLPPNLGRP(GLOBAL DCAHP)
     A* ------------------------------------------------------------------------
     A* Readonly-Display 1 -----------------------------------------------------
     A          R DSPHST1
     A                                      CA03(03 'Verlassen')
     A                                      CA10(10 'Vlan-Info')
     A                                      CA12(12 'Abbrechen')
     A                                      PAGEUP(25)
     A                                      PAGEDOWN(26)
     A                                      CHGINPDFT
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('Cisco-Wartungen: -
     A                                      Erfasste Daten')
     A          H                           HLPARA(3 2 3 68)
     A                                      HLPPNLGRP('DCA/HOSTNAME' DCAHP)
     A          H                           HLPARA(4 2 4 34)
     A                                      HLPPNLGRP('DCA1/DCA' DCAHP)
     A          H                           HLPARA(5 2 5 34)
     A                                      HLPPNLGRP('DCA1/UPD_IGN' DCAHP)
     A          H                           HLPARA(6 2 6 34)
     A                                      HLPPNLGRP('DCA1/ACU_IGN' DCAHP)
     A          H                           HLPARA(8 2 8 8)
     A                                      HLPPNLGRP('DCA1/ACCESS' DCAHP)
     A          H                           HLPARA(9 2 9 33)
     A                                      HLPPNLGRP('DCA1/SERVTYP' DCAHP)
     A          H                           HLPARA(10 2 10 36)
     A                                      HLPPNLGRP('DCA1/CONN' DCAHP)
     A          H                           HLPARA(11 2 11 46)
     A                                      HLPPNLGRP('DCA1/USERNAME' DCAHP)
     A          H                           HLPARA(12 2 12 46)
     A                                      HLPPNLGRP('DCA1/PASSWD' DCAHP)
     A          H                           HLPARA(13 2 13 46)
     A                                      HLPPNLGRP('DCA1/ENABLE' DCAHP)
     A          H                           HLPARA(15 2 16 52)
     A                                      HLPPNLGRP('DCA1/COMMT' DCAHP)
     A          H                           HLPARA(18 2 18 12)
     A                                      HLPPNLGRP('DCA1/STAMPS' DCAHP)
     A          H                           HLPARA(19 2 19 80)
     A                                      HLPPNLGRP('DCA1/STAMP' DCAHP)
     A          H                           HLPARA(20 2 20 46)
     A                                      HLPPNLGRP('DCA1/CFUPDT' DCAHP)
     A          H                           HLPARA(21 2 21 80)
     A                                      HLPPNLGRP('DCA1/CFSAVD' DCAHP)
     A          H                           HLPARA(22 70 22 80)
     A                                      HLPPNLGRP('DCA1/MORE' DCAHP)
     A          H                           HLPARA(23 1 23 45)
     A                                      HLPPNLGRP('DCA/FKEYS' DCAHP)
     A*         ##End of definitions. Here be real screen elements.
     A                                  1 24'Cisco-Wartungen: Erfasste Daten'
     A                                      DSPATR(HI)
     A                                  3  2'Hostname  . . . . . . . . .'
     A                                      DSPATR(HI)
     A            HOSTNAME  R           3 31REFFLD(HOSTNAME HSTPF)
     A                                  4  3'Automatische Erfassung'
     A N40                              4 31'Nein'
     A  40                              4 31'Ja'
     A                                  5  3'Ignoriere neues OS . . . .'
     A N41                              5 31'Nein'
     A  41                              5 31'Ja'
     A                                  6  3'Ignoriere neuen Anyconnect'
     A N42                              6 31'Nein'
     A  42                              6 31'Ja'
     A                                      COLOR(TRQ)
     A                                  8  2'Zugriff' DSPATR(HI)
     A                                  9  3'Hosttyp  . . . . . . . . .'
     A            SERVTYP   R           9 31REFFLD(SERVTYP HSTPF)
     A                                 10  3'Zugriffsmethode  . . . . .'
     A            CONN      R          10 31REFFLD(CONN HSTPF)
     A                                 11  3'Benutzer . . . . . . . . .'
     A            USERNAME  R          11 31REFFLD(USERNAME HSTPF)
     A                                 12  3'Passwort . . . . . . . . .'
     A            PASSWD    R          12 31REFFLD(PASSWD HSTPF)
     A  43                             13  3'Enable-Secret  . . . . . .'
     A  43        ENABLE    R          13 31REFFLD(ENABLE HSTPF)
     A                                 15  2'Bemerkung' DSPATR(HI)
     A            COMMT     R          16  3REFFLD(COMMT HSTPF)
     A                                 18  2'Zeitstempel' DSPATR(HI)
     A                                 19  3'Erfassung  . . . . . . . .'
     A            STAMP$        16A    19 31
     A  44                             19 53'Erfassung älter als 3 Tage!'
     A                                      COLOR(RED)
     A  45                             20  3'Cfg Geändert (run) . . . .'
     A  45        CFUPDT$       16A    20 31
     A  46                             21  3'Cfg Gesichert (save) . . .'
     A  46        CFSAVD$       16A    21 31
     A  47                             21 53'Älter als run, wr vergessen!'
     A                                      COLOR(RED)
     A                                 22 70'Weitere ...' DSPATR(HI)
     A                                 23  2'F3=Verlassen   F10=Vlan-Info   -
     A                                      F12=Abbrechen'
     A                                      COLOR(BLU)
     A* Readonly-Display 2 -----------------------------------------------------
     A          R DSPHST2
     A                                      CA03(03 'Verlassen')
     A                                      CA10(10 'Vlan-Info')
     A                                      CA12(12 'Abbrechen')
     A                                      PAGEUP(25)
     A                                      PAGEDOWN(26)
     A                                      CHGINPDFT
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('Cisco-Wartungen: -
     A                                      Erfasste Daten')
     A          H                           HLPARA(3 2 3 68)
     A                                      HLPPNLGRP('DCA/HOSTNAME' DCAHP)
     A          H                           HLPARA(5 2 5 9)
     A                                      HLPPNLGRP('DCA2/HARDWARE' DCAHP)
     A          H                           HLPARA(6 2 6 70)
     A                                      HLPPNLGRP('DCA2/MODEL' DCAHP)
     A          H                           HLPARA(7 2 7 39)
     A                                      HLPPNLGRP('DCA2/RAM' DCAHP)
     A          H                           HLPARA(7 56 7 80)
     A                                      HLPPNLGRP('DCA2/FLASH' DCAHP)
     A          H                           HLPARA(8 2 8 42)
     A                                      HLPPNLGRP('DCA2/SERNO' DCAHP)
     A          H                           HLPARA(10 2 10 5)
     A                                      HLPPNLGRP('DCA2/SOFTWARE' DCAHP)
     A          H                           HLPARA(11 2 11 80)
     A                                      HLPPNLGRP('DCA2/VERSION' DCAHP)
     A          H                           HLPARA(12 2 12 50)
     A                                      HLPPNLGRP('DCA2/ROMVER' DCAHP)
     A          H                           HLPARA(13 2 13 50)
     A                                      HLPPNLGRP('DCA2/ASA_DM_VER' DCAHP)
     A          H                           HLPARA(14 2 15 80)
     A                                      HLPPNLGRP('DCA2/SYSIMG' DCAHP)
     A          H                           HLPARA(16 2 17 80)
     A                                      HLPPNLGRP('DCA2/RLDREASON' DCAHP)
     A          H                           HLPARA(18 2 19 80)
     A                                      HLPPNLGRP('DCA2/UPTIME' DCAHP)
     A          H                           HLPARA(19 2 20 36)
     A                                      HLPPNLGRP('DCA2/CONFREG' DCAHP)
     A          H                           HLPARA(20 31 21 77)
     A                                      HLPPNLGRP('DCA2/ASA_FOVER' DCAHP)
     A          H                           HLPARA(22 77 22 80)
     A                                      HLPPNLGRP('DCA2/END' DCAHP)
     A          H                           HLPARA(23 1 23 45)
     A                                      HLPPNLGRP('DCA/FKEYS' DCAHP)
     A*         ##End of definitions. Here be real screen elements.
     A                                  1 24'Cisco-Wartungen: Erfasste Daten'
     A                                      DSPATR(HI)
     A                                  3  2'Hostname  . . . . . . . . .'
     A                                      DSPATR(HI)
     A            HOSTNAME  R           3 31REFFLD(HOSTNAME HSTPF)
     A                                      COLOR(TRQ)
     A                                  5  2'Hardware' DSPATR(HI)
     A                                  6  3'Modell . . . . . . . . . .'
     A            MODEL     R           6 31
     A  50                              6 56'End of Support!' COLOR(RED)
     A                                  7  3'RAM  . . . . . . . . . . .'
     A            RAM       R     Y     7 32EDTCDE(1)
     A                                  7 38'MB'
     A                                  7 56'Flash . . . . .'
     A            FLASH     R     Y     7 72EDTCDE(1)
     A                                  7 79'MB'
     A                                  8  3'Seriennummer . . . . . . .'
     A            SERNO     R           8 31
     A                                 10  2'Software' DSPATR(HI)
     A                                 11  3'Version  . . . . . . . . .'
     A            VERSION   R          11 31
     A  51                             11 56'Upd.' COLOR(TRQ)
     A  51        VER_SOLL  R          11 61REFFLD(VERSION) COLOR(TRQ)
     A  52                             12  3'ROM-Version  . . . . . . .'
     A  52        ROMVER    R          12 31
     A  53                             13  3'ASDM-Version . . . . . . .'
     A  53        ASA_DM_VERR          13 31
     A                                 14  3'OS-Image . . . . . . . . .'
     A            SYSIMG    R        B 14 31CNTFLD(50) DSPATR(PR)
     A  54                             16  3'Reload Reason  . . . . . .'
     A  54        RLD$REASONR        B 16 31CNTFLD(50) DSPATR(PR)
     A  55                                  COLOR(RED)
     A                                 19  3'Uptime . . . . . . . . . .'
     A            UPTIME    R          19 31
     A                                 20  3'Config-Register  . . . . .'
     A            CONFREG   R          20 31
     A  56                             21 31'Achtung!'
     A                                      COLOR(YLW) DSPATR(RI)
     A  56                             21 40'Host ist Teil eines Failover--
     A                                      Clusters!'
     A                                 22 75'Ende' DSPATR(HI)
     A                                 23  2'F3=Verlassen   F10=Vlan-Info   -
     A                                      F12=Abbrechen'
     A                                      COLOR(BLU)
     A* ------------------------------------------------------------------------
     A* Vlan Subfile -----------------------------------------------------------
     A          R MAINSFL                   SFL
     A*         ##Pull values from the table definition itself...
     A            VLAN      R     Y  O 11  2EDTCDE(3) REFFLD(VLAN VLANPF)
     A            DESCR     R        O 11  8REFFLD(DESCR VLANPF)
     A* Subfile Control Screen -------------------------------------------------
     A          R MAINCTL                   SFLCTL(MAINSFL)
     A                                      WINDOW(2 37 20 39 *NOMSGLIN)
     A                                      WDWTITLE((*TEXT 'Vlan-Konfigura-
     A                                      tion') *CENTER (*COLOR GRN))
     A*                                   ##Command Key definitions.
     A                                      CA03(03 'Verlassen')
     A                                      CA12(12 'Abbrechen')
     A*                                   ##Basic Settings for subfile ctrl.
     A                                      OVERLAY
     A                                      SFLSIZ(14)
     A                                      SFLPAG(7)
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND(*MORE)
     A*         ##Help Area definition for this screen.
     A                                      HLPTITLE('Vlan-Konfiguration')
     A          H                           HLPARA(2 1 2 17)
     A                                      HLPPNLGRP('VLAN/VTPCFG' DCAHP)
     A          H                           HLPARA(3 1 3 39)
     A                                      HLPPNLGRP('VLAN/VTPDOM' DCAHP)
     A          H                           HLPARA(4 1 4 39)
     A                                      HLPPNLGRP('VLAN/VTPMODE' DCAHP)
     A          H                           HLPARA(5 1 5 39)
     A                                      HLPPNLGRP('VLAN/VTPVERS' DCAHP)
     A          H                           HLPARA(6 1 6 39)
     A                                      HLPPNLGRP('VLAN/VTPPRUNE' DCAHP)
     A          H                           HLPARA(8 1 8 17)
     A                                      HLPPNLGRP('VLAN/VLANS' DCAHP)
     A          H                           HLPARA(*CNST 1)
     A                                      HLPPNLGRP('VLAN/VLAN' DCAHP)
     A          H                           HLPARA(*CNST 2)
     A                                      HLPPNLGRP('VLAN/NAME' DCAHP)
     A          H                           HLPARA(11 1 18 39)
     A                                      HLPPNLGRP('VLAN/SFLDTA' DCAHP)
     A            SFLRCDNBR      2S 0H
     A*         ##End of definitions. Here be real screen elements.
     A                                  2  1'VTP Konfiguration' DSPATR(HI)
     A                                  3  2'Domain  . . .'
     A            VTP_DOMAINR           3 16
     A                                  4  2'Mode  . . . .'
     A            VTP_MODE  R           4 16
     A                                  5  2'Version . . .'
     A            VTP_VERS  R           5 16EDTCDE(1)
     A  57                              6  2'Pruning . . .'
     A  57N58                           6 16'Nein'
     A  57 58                           6 16'Ja'
     A                                  8  1'Lokale Vlan-Liste' DSPATR(HI)
     A                                 10  4'ID' DSPATR(HI) HLPID(1)
     A                                 10  8'Name' DSPATR(HI) HLPID(2)
     A* Footer of the form -----------------------------------------------------
     A* A subfile control record may contain elements above or below the actual
     A* subfile, but not both.
     A          R MAINBTM                   WINDOW(MAINCTL)
     A                                      HLPTITLE('Vlan-Konfiguration')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('VLAN/SFLBTM' DCAHP)
     A                                 20  2'F1=Hilfe  -
     A                                      F3=Verlassen  -
     A                                      F12=Abbrechen'
     A                                      COLOR(BLU)
     A* No data instead of SFL -------------------------------------------------
     A          R MAINND                    WINDOW(MAINCTL)
     A                                      OVERLAY
     A                                      HLPTITLE('Vlan-Konfiguration')
     A          H                           HLPARA(*RCD)
     A                                      HLPPNLGRP('VLAN/SFLND' DCAHP)
     A                                 11  2'(Keine Vlans gefunden.)'
     A* For vim ----------------------------------------------------------------
     A* vim: syntax=dds colorcolumn=81 autoindent noignorecase
